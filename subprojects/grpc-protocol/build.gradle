buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("com.google.protobuf:protobuf-gradle-plugin:0.8.8")
    }
}

apply plugin: 'org.springframework.boot'
apply plugin: 'com.google.protobuf'

jar.enabled = true
bootJar.enabled = false

dependencies {
    implementation "io.grpc:grpc-protobuf:${grpcVersion}"

    // examples/advanced need this for JsonFormat
    implementation "com.google.protobuf:protobuf-java-util:${protobufVersion}"

    implementation "io.grpc:grpc-stub:${grpcVersion}"
    implementation "com.fasterxml.jackson.module:jackson-module-kotlin"
    compileOnly "javax.annotation:javax.annotation-api:1.2"

    testImplementation "io.grpc:grpc-testing:${grpcVersion}"
}

idea {
    module {
        sourceDirs += file("${projectDir}/src/main/proto")
        testSourceDirs += file("${projectDir}/src/test/proto")
    }
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.5.1-1"
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
}

sourceSets {
    main {
        java {
            srcDir "build/generated/source/proto/main/java"
            srcDir "build/generated/source/proto/main/grpc"
        }
    }

    test {
        java {
            srcDir "build/generated/source/proto/test/java"
            srcDir "build/generated/source/proto/test/grpc"
        }
    }
}